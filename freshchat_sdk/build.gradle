apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

group = 'com.github.freshdesk'

def static getEscapedString(value) {
    return "\"$value\"";
}

def getScmRevision = {
    String rev = 'git rev-parse --short HEAD'.execute().text.trim()
    return getEscapedString(rev)
}

def getScmBranch = {
    String branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
    return getEscapedString(branch)
}

def localProperties = {
    Properties properties = new Properties()
    File file = new File(rootProject.rootDir, "local.properties")
    if (file.exists()) {
        properties.load(new FileInputStream(file))
    }
    return properties
}

android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 16
        versionCode 541
        versionName "5.4.1"
        targetSdkVersion 31
        consumerProguardFiles 'consumer-proguard-rules.pro'
        buildConfigField "long", 'BUILT_AT', System.currentTimeMillis() + 'L'
        buildConfigField "String", "SCM_REV", getScmRevision()
        buildConfigField "String", "SCM_BRANCH", getScmBranch()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'sdk-proguard-rules.pro'
        }
    }
}

publishing {
    Properties properties = localProperties()
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/pomelofashion/mobile-android")
            credentials {
                username = properties["gpr.user"]
                password = properties["gpr.key"]
            }
        }
    }
    publications {
        aar(MavenPublication) {
            groupId = 'com.pomelofashion'
            artifactId = 'freshchat-android'
            version = properties["artifact.version"]
            artifact('build/outputs/aar/freshchat_sdk-release.aar')
        }
    }
}


ext {
    gsonVersion             = '2.8.0'
    picassoVersion          = '2.8'
    circleImageViewVersion  = '3.1.0'
    appcompatVersion        = '1.0.0'
    recyclerviewVersion     = '1.0.0'
    materialVersion         = '1.0.0'
    cardviewVersion         = '1.0.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerviewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.cardview:cardview:$cardviewVersion"
    implementation "de.hdodenhof:circleimageview:$circleImageViewVersion"
}
